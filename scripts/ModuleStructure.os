//@script_name ModuleStructure
//@script_display_name Структура модуля
//@script_description Вставляет в модуль шаблоны областей по стандартам разработки 1С
//@script_author nytlenc
//@script_developer_url https://infostart.ru/profile/44476/
//@script_url https://turboconf.ru/Forum/Details/1524#ModuleStructure
//@script_section Редактирование
//@script_hotkey Alt+P ПоказатьФорму Структура модуля
//@turbomenu 1
//@turbobutton 1
//@retain_clipboard 1
//@enterprise_mode 0
//@hide_actions 1
//@script_version 4

Перем ИменаОбластей;

Процедура ПоказатьФорму()

	ТК = Новый ТурбоКонф;

	Поз = 0;
	ВыделенныйТекст = "";
	
	Текст = ТК.ПолучитьТекстМодуля(ВыделенныйТекст, Поз);
	
	Если ВыделенныйТекст = "" Тогда
		Пункты = Новый Соответствие;
		Пункты.Вставить("Общий модуль",       "1");
		Пункты.Вставить("Модуль объекта",     "2");
		Пункты.Вставить("Модуль формы",       "3");
		Пункты.Вставить("Модуль команды",     "4");
		
		Форма = Новый ФормаВыбораЗначения;
		Форма.Заголовок            = "Укажите набор шаблонов";
		Форма.Данные               = Пункты;
		Форма.АвтоматическийРазмер = Истина;
		Форма.УстановитьДействие(ЭтотОбъект,  "ВставитьШаблон");
		Форма.Показать();
	Иначе
		Пункты = Новый Соответствие;
		Для Каждого ИмяОбласти Из ИменаОбластей Цикл
			Пункты.Вставить(ИмяОбласти.Значение, ИмяОбласти.Ключ);
		КонецЦикла;
		
		Форма  = Новый ФормаВыбораЗначения;
		Форма.Заголовок            = "Укажите область";
		Форма.Данные               = Пункты;
		Форма.АвтоматическийРазмер = Истина;
		Форма.УстановитьДействие(ЭтотОбъект,  "ОкружитьОбластью");
		Форма.Показать();
	КонецЕсли;

КонецПроцедуры

Процедура ВставитьШаблон(Значение, Отказ) Экспорт

	Если ПустаяСтрока(Значение) Тогда
		Возврат;
	КонецЕсли;

	ТК = Новый ТурбоКонф;

	Поз = 0;
	ВыделенныйТекст = "";
	
	Текст = ТК.ПолучитьТекстМодуля(ВыделенныйТекст, Поз);

	Если Значение = "1" Тогда
		Шаблон =
			"////////////////////////////////////////////////////////////////////////////////
			|// <Заголовок модуля: краткое описание и условия применения модуля.>
			|//  
			|////////////////////////////////////////////////////////////////////////////////
			|#Область ПрограммныйИнтерфейс
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область СлужебныйПрограммныйИнтерфейс
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область СлужебныеПроцедурыИФункции
			|//Код процедур и функций
			|#КонецОбласти";
	ИначеЕсли Значение = "2" Тогда
		Шаблон =
			"#Область ПрограммныйИнтерфейс
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область ОбработчикиСобытий
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область СлужебныйПрограммныйИнтерфейс
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область СлужебныеПроцедурыИФункции
			|//Код процедур и функций
			|#КонецОбласти";
	ИначеЕсли Значение = "3" Тогда
		Шаблон =
			"#Область ОбработчикиСобытийФормы
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область ОбработчикиСобытийЭлементовШапкиФормы
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область ОбработчикиСобытийЭлементовТаблицыФормы<ИмяТаблицыФормы>
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область ОбработчикиКомандФормы
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область СлужебныеПроцедурыИФункции
			|//Код процедур и функций
			|#КонецОбласти";
	ИначеЕсли Значение = "4" Тогда
		Шаблон =
			"#Область ОбработчикиСобытий
			|//Код процедур и функций
			|#КонецОбласти
			|
			|#Область СлужебныеПроцедурыИФункции
			|//Код процедур и функций
			|#КонецОбласти";
	КонецЕсли;

	Шаблон = Шаблон + Символы.ПС;

	ТК.КонтролКлавиша(Клавиши.Home);
	ТК.ВставитьТекст(Шаблон);

КонецПроцедуры

Процедура ОкружитьОбластью(Значение, Отказ) Экспорт

	Если ПустаяСтрока(Значение) Тогда
		Возврат;
	КонецЕсли;

	ТК = Новый ТурбоКонф;
	
	Поз = 0;
	ВыделенныйТекст = "";
	Текст = ТК.ПолучитьТекстМодуля(ВыделенныйТекст, Поз);

	ИмяОбласти = ИменаОбластей.Получить(Значение);

	Если ИмяОбласти = "<ЗадатьСобственноеИмяОбласти>" Тогда
		Форма = Новый ФормаВводаЗначения;
		Форма.УстановитьДействие(ЭтотОбъект, "ОбработчикВводаИмениОбласти");
		Форма.Заголовок = "Ввод имени области";
		Форма.Текст = "Введите имя области"+Символы.ПС;
		Форма.Значение = "СобственноеИмяОбласти";
		Форма.Показать();
		Возврат;
	КонецЕсли;

	Шаблон = 
		"#Область %ИмяОбласти%
		|
		|%ВыделенныйТекст%
		|
		|#КонецОбласти";

	ТекстДляВставки = СтрЗаменить(Шаблон, "%ИмяОбласти%", ИмяОбласти);
	ТекстДляВставки = СтрЗаменить(ТекстДляВставки, "%ВыделенныйТекст%", СокрЛП(ВыделенныйТекст));
	ТК.ВставитьТекст(ТекстДляВставки);

КонецПроцедуры

Процедура ОбработчикВводаИмениОбласти(Значение, Отказ) Экспорт

	ТК = Новый ТурбоКонф;
	
	Поз = 0;
	ВыделенныйТекст = "";
	Текст = ТК.ПолучитьТекстМодуля(ВыделенныйТекст, Поз);

	Шаблон = 
		"#Область "+Значение+"
		|
		|%ВыделенныйТекст%
		|
		|#КонецОбласти";

	ТекстДляВставки = СтрЗаменить(Шаблон, "%ВыделенныйТекст%", СокрЛП(ВыделенныйТекст));
	ТК.ВставитьТекст(ТекстДляВставки);

КонецПроцедуры

ИменаОбластей = Новый Соответствие;
ИменаОбластей.Вставить("1", "ПрограммныйИнтерфейс");
ИменаОбластей.Вставить("2", "СлужебныйПрограммныйИнтерфейс");
ИменаОбластей.Вставить("3", "ОбработчикиСобытий");
ИменаОбластей.Вставить("4", "ОбработчикиСобытийФормы");
ИменаОбластей.Вставить("5", "ОбработчикиСобытийЭлементовШапкиФормы");
ИменаОбластей.Вставить("6", "ОбработчикиСобытийЭлементовТаблицыФормы<ИмяТаблицыФормы>");
ИменаОбластей.Вставить("7", "ОбработчикиКомандФормы");
ИменаОбластей.Вставить("8", "СлужебныеПроцедурыИФункции");
ИменаОбластей.Вставить("9", "<ЗадатьСобственноеИмяОбласти>");