//@script_name Zakryvashka
//@script_display_name Автозакрытие окон
//@script_description Скрипт автоматически закрывает некоторые надоедливые предупреждающие окна конфигуратора
//@script_author m.bolsun
//@script_developer_url http://turboconf.ru
//@script_url https://turboconf.ru/Forum/Details/3744#Zakryvashka
//@script_section Глобальные
//@script_hotkey None Настройки Настройка автозакрытия окон
//@turbomenu 1
//@trigger 1
//@filter ^(Конфигуратор|Объект перехода)$
//@retain_clipboard 0
//@enterprise_mode 0
//@hide_actions 0
//@script_version 3

Перем Настройки;
Перем ТК;
Перем ПредупреждениеОбОшибках;
Перем ПредупреждениеРедактируемаяКонфигурацияОтличается;

Процедура ОбработчикОтветаПользователяСброситьВсе(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ТК.УдалитьЗначение("АвтозакрытиеОкон_ВопросОбОтключенииСкриптаЗадан");
		
		Предупреждения = ЗаполнитьПредупреждения();

		Для Каждого Предупреждение Из Предупреждения Цикл
		
			ТК.УдалитьЗначение("АвтозакрытиеОкон_"+Предупреждение);
	
		КонецЦикла;

	КонецЕсли;

	Настройки();
	
КонецПроцедуры

Процедура ОбработчикВыбораЗначения(Значение, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если Значение = "СброситьВсе" Тогда
		
		ВопросПользователю = Новый ВопросПользователю(РежимДиалогаВопрос.ДаНетОтмена);
		
		ВопросПользователю.Заголовок = "Автозакрытие окон"; //Необязательно
		ВопросПользователю.Текст = "Сбросить настройки для всех типов предупреждений?";
		ВопросПользователю.УстановитьОбработчик(ЭтотОбъект, "ОбработчикОтветаПользователяСброситьВсе");
		
		ВопросПользователю.Показать();

		Возврат;
	
	ИначеЕсли Значение = "Выход" Тогда

		Возврат;		

	Иначе
		
		СохраненноеЗначение = ТК.ВосстановитьЗначение("АвтозакрытиеОкон_"+Значение);

		Если СохраненноеЗначение = Истина Тогда
			ТК.СохранитьЗначение("АвтозакрытиеОкон_"+Значение, Ложь);
		Иначе
			ТК.СохранитьЗначение("АвтозакрытиеОкон_"+Значение, Истина);
		КонецЕсли;

		Настройки();

	КонецЕсли;	

КонецПроцедуры

Процедура Настройки() Экспорт
	
	ТК = Новый ТурбоКонф;
	
	Форма = Новый ФормаВыбораЗначения();
	Форма.УстановитьДействие(ЭтотОбъект, "ОбработчикВыбораЗначения");

	Предупреждения = ЗаполнитьПредупреждения();

	Пункты = Новый Соответствие;

	Для Каждого Предупреждение Из Предупреждения Цикл
		
		Значение = ТК.ВосстановитьЗначение("АвтозакрытиеОкон_"+Предупреждение);

		Представление = СтрЗаменить(Предупреждение, Символы.ПС, " ");

		Если Значение = Истина Тогда
			Представление = Представление + " (включено)";
		ИначеЕсли Значение = Ложь Тогда
			Представление = Представление + " (отключено)";
		Иначе
			Представление = Представление + " (не задано)";			
		КонецЕсли;
		
		Пункты.Вставить(Представление, Предупреждение);	

	КонецЦикла;

	Пункты.Вставить("Сбросить все", "СброситьВсе");
	Пункты.Вставить("Выход", "Выход");
	
	Форма.Данные = Пункты;
	Форма.Заголовок = "Настройка автозакрытия окон";
	Форма.АвтоматическийРазмер = Истина;
	
	Форма.Показать();

КонецПроцедуры

Процедура ОбработчикОтветаПользователя(Результат, Параметры) Экспорт

	ТК.СохранитьЗначение("АвтозакрытиеОкон_ВопросОбОтключенииСкриптаЗадан", Истина);

	Если Результат = КодВозвратаДиалога.Да Тогда
	
		ВыполнитьСкрипт();
	
	Иначе		

		ТК.EnableScript("Zakryvashka", false);

	КонецЕсли;

КонецПроцедуры

Процедура ОбработчикОтветаПользователяТипПредупреждения(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
	
		ТК.СохранитьЗначение("АвтозакрытиеОкон_"+Параметры, Истина);

		ТК.Клавиша(Keys.Enter);

	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
	
		ТК.СохранитьЗначение("АвтозакрытиеОкон_"+Параметры, Ложь);

	КонецЕсли;

КонецПроцедуры

Функция ЗаполнитьПредупреждения()

	Предупреждения = Новый Массив();

	ПредупреждениеОбОшибках = "При проверке модуля обнаружены ошибки!
	|Продолжить?";

	Предупреждения.Добавить(ПредупреждениеОбОшибках);	
	Предупреждения.Добавить("Приложение запущено. Перезапустить?");	
	Предупреждения.Добавить("Для обновления конфигурации базы данных необходимо прекратить отладку.
		|Продолжить?");
	Предупреждения.Добавить("Связь с сервером отладки разорвана");
	Предупреждения.Добавить("Объект перехода");
	
	ПредупреждениеРедактируемаяКонфигурацияОтличается = "Редактируемая конфигурация отличается от конфигурации БД. Обновить конфигурацию БД?";
	
	Предупреждения.Добавить(ПредупреждениеРедактируемаяКонфигурацияОтличается);

	Возврат Предупреждения;

КонецФункции

Процедура ВыполнитьСкрипт()

	Предупреждения = ЗаполнитьПредупреждения();

	Для Каждого Предупреждение Из Предупреждения Цикл

		Если Предупреждение = "Объект перехода" И ТК.ПолучитьЗаголовокТекущегоОкна() = "Объект перехода" Тогда
			
			ПроверитьПредупреждениеИЗакрыть(Предупреждение);

			Возврат;

		КонецЕсли;
		
		Результат = ТК.НайтиЭлементПоИмени(Предупреждение, "панель");

		Если Результат Тогда
			
			ПроверитьПредупреждениеИЗакрыть(Предупреждение);

			Возврат;
		
		КонецЕсли;

	КонецЦикла;

	Если ТК.ВосстановитьЗначение("АвтозакрытиеОкон_"+ПредупреждениеРедактируемаяКонфигурацияОтличается) <> Ложь Тогда

		Элементы = ТК.НайтиЭлементыПоТипу("панель");

		Для каждого Элемент Из Элементы Цикл
			
			Если ПустаяСтрока(Элемент) Тогда
				Продолжить;
			КонецЕсли;

			Если СтрНачинаетсяС(Элемент, "Редактируемая конфигурация") Тогда			
				
				ПроверитьПредупреждениеИЗакрыть(ПредупреждениеРедактируемаяКонфигурацияОтличается);

				Возврат;
				
							
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;	
	
КонецПроцедуры

Процедура ПроверитьПредупреждениеИЗакрыть(ТипПредупреждения)

	Значение = ТК.ВосстановитьЗначение("АвтозакрытиеОкон_"+ТипПредупреждения);

	Если Значение = Неопределено Тогда
		
		ВопросПользователю = Новый ВопросПользователю(РежимДиалогаВопрос.ДаНет);

		ВопросПользователю.Заголовок = "Автозакрытие окон";
		ВопросПользователю.Текст = "Закрывать подобные предупреждения конфигуратора автоматически?";
		//Для некоторых сообщений вместо штатного модального окна, будет показано всплывающее уведомление. 
		ВопросПользователю.УстановитьОбработчик(ЭтотОбъект, "ОбработчикОтветаПользователяТипПредупреждения");
		ВопросПользователю.Параметры = ТипПредупреждения;

		ВопросПользователю.Показать();

	ИначеЕсли Значение = Истина Тогда
	
		ТК.Клавиша(Keys.Enter);

		Если ТипПредупреждения = ПредупреждениеОбОшибках Тогда
			ТК.ПоказатьВсплывающееУведомление("Внимание!", "Модуль сохранен, но при проверке обнаружены ошибки!", 3000);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

ТК = Новый ТурбоКонф;

ТК.Ждать(100);

Значение = ТК.ВосстановитьЗначение("АвтозакрытиеОкон_ВопросОбОтключенииСкриптаЗадан");

Если Значение = Неопределено Тогда
	
	ВопросПользователю = Новый ВопросПользователю(РежимДиалогаВопрос.ДаНет);

	ВопросПользователю.Заголовок = "Автозакрытие окон";
	ВопросПользователю.Текст = "Включить скрипт автозакрытия предупреждений конфигуратора?
	|Для каждого типа предупреждения будет задан отдельный вопрос.
	|Вкл./откл. скрипт можно позже в Настройках, вкладка Скрипты.
	|Настроить предупреждения ТурбоМеню/Скрипты пункт Настройка автозакрытия";
	ВопросПользователю.УстановитьОбработчик(ЭтотОбъект, "ОбработчикОтветаПользователя");

	ВопросПользователю.Показать();

Иначе
	
	ВыполнитьСкрипт();	

КонецЕсли;